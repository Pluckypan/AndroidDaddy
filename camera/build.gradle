import echo.DateAndTimePlugin

apply plugin: 'com.android.application'
apply plugin: 'android-aspectjx'
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId 'com.echo.camera'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
                arguments = ['hugox.version': '1.0.0',
                             'hugox.name'   : 'Sofia',
                             'hugox.age'    : '27']
            }
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    annotationProcessor project(':HugoXCompiler')
    implementation project(':HugoXAnno')
    // for test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
aspectjx {
    enabled true
    //include "echo.xxx.*"
    //exclude 'android.support'
}

// build.gradle 中的插件编写
class TimePrintPlugin implements Plugin<Project> {

    @Override
    void apply(Project project) {

        project.extensions.create("TimePrintFormat", TimePrintFormatExtension)

        project.task('showTime') << {
            println "Current time is " + new Date().format(project.TimePrintFormat.timeFormat)
        }

        project.tasks.create('showDate') << {
            println "Current date is " + new Date().format(project.TimePrintFormat.dateFormat)
        }
    }
}

class TimePrintFormatExtension {
    String timeFormat = "MM/dd/yyyy HH:mm:ss.SSS"
    String dateFormat = "yyyy-MM-dd"
}

apply plugin: TimePrintPlugin

TimePrintFormat {
    timeFormat = 'HH:mm:ss.SSS'
    dateFormat = 'MM/dd/yyyy'
}

apply plugin: DateAndTimePlugin

dateAndTime {
    timeFormat = 'HH:mm:ss.SSS'
    dateFormat = 'MM/dd/yyyy'
}

buildscript {
    repositories {
        maven {
            //方式一：本地文件夹
            url 'file:../localPublish'

            //Maven仓库地址
            //url 'http://localhost:8081/nexus/content/repositories/thirdparty/'
        }
    }
    dependencies {
        classpath 'engineer.echo:hugox-plugin:1.1.0'
    }
}

apply plugin: 'hugox-plugin'
HugoX {
    enable = true
    version = "1.0.2"
}